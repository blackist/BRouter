apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.sourceCompatibility
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
}

group = POM_GROUP
version = POM_VERSION
project.archivesBaseName = POM_ARTIFACTID

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://androiddoc.qiniudn.com/reference/packages.html"
        title "$POM_NAME $POM_VERSION"
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name POM_NAME
            description POM_DESCRIPTION
            url POM_URL
            inceptionYear '2018'

            packaging POM_PACKAGING
            groupId POM_GROUP
            artifactId POM_ARTIFACTID
            version POM_VERSION

            licenses {
                license {
                    name POM_LICENCE_NAME
                    url POM_LICENCE_URL
                    distribution POM_LICENCE_DIST
                }
            }

            scm {
                connection POM_SCM_CONNECTION
                url POM_SCM_URL
            }

            developers {
                developer {
                    id POM_DEVELOPER_ID
                    name POM_DEVELOPER_NAME
                    email POM_DEVELOPER_EMAIL
                }
            }
        }
    }
}

bintray {
    java.util.Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty('BINTRAY_USER')
    key = properties.getProperty('BINTRAY_KEY')
    configurations = ['archives']

    dryRun = false
    publish = true

    pkg {
        repo = 'arr'
        name = POM_NAME
        licenses = ['Apache-2.0']
        vcsUrl = POM_SCM_CONNECTION
        websiteUrl = POM_URL
        issueTrackerUrl = POM_ISSUE_URL
        publicDownloadNumbers = true
        version {
            name = POM_VERSION
            desc = POM_DESCRIPTION
            vcsTag = POM_VERSION

            gpg {
                sign = true
            }
        }
    }
}
